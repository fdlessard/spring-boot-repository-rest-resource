plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '3.1.1'
}

group = 'io.fdlessard.codebites.customer'
version = '0.0.1-SNAPSHOT'
archivesBaseName = 'spring-boot-repository-rest-resource'
sourceCompatibility = '15'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

springBoot  {
    buildInfo()
}

bootBuildImage {
    imageName = "fdlessard/${project.name}:0.0.1-SNAPSHOT"
}

repositories {
    mavenCentral()
}

ext {
    jacocoToolVersion = '0.8.6'
    jacocoInstructionCoverageMinimum = 0.90
    jacocoLineCoverageMinimum = 0.90
    jacocoMethodCoverageMinimum = 0.90
    jacocoClassCoverageMinimum = 0.90
    jacocoBranchCoverageMinimum = 0.85
    jacocoComplexityCoverageMinimum = 0.50
    set('testcontainersVersion', "1.15.1")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.apache.commons:commons-lang3:3.9'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    implementation 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core:4.2.2'
    implementation 'pl.project13.maven:git-commit-id-plugin:4.0.2'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.projectlombok:lombok'
    testImplementation 'org.projectlombok:lombok'
    integrationTestImplementation 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok:'
    integrationTestAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.5.1'

    testImplementation 'org.testcontainers:junit-jupiter:1.15.1'
    testImplementation 'org.testcontainers:postgresql:1.15.1'
    testImplementation 'org.testcontainers:localstack:1.15.1'

    integrationTestImplementation 'org.projectlombok:lombok'
    integrationTestAnnotationProcessor 'org.projectlombok:lombok'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

test {
    useJUnitPlatform()
    check.dependsOn jacocoTestCoverageVerification
    finalizedBy jacocoTestReport
    testLogging {
        exceptionFormat = 'full'
        showExceptions true
        showCauses true
        showStackTraces true
        events 'passed', "skipped", 'failed', 'standardError', 'standardOut'
    }
    reports {
        junitXml.enabled = true
        junitXml.outputLocation = file("build/test-results/unitTest")
        html.enabled = true
        html.destination = file("build/reports/tests/unitTest")
        binaryResultsDirectory = file("build/test-results/unitTest/binary")
    }
    jacoco {
        enabled = true
        ignoreFailures = false
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nUnit Test result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

sourceSets {
    integrationTest {
        java.srcDir 'src/it/java'
        resources.srcDir 'src/it/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output + sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.integrationTest.output + sourceSets.integrationTest.compileClasspath  + sourceSets.test.runtimeClasspath
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs the integration tests.'
    group = 'verification'
    classpath = sourceSets.integrationTest.runtimeClasspath
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    outputs.upToDateWhen { false }
    mustRunAfter test
    check.dependsOn integrationTest // if you only want unit test, comment
    testLogging {
        exceptionFormat = 'full'
        showExceptions true
        showCauses true
        showStackTraces true
        events 'passed', "skipped", 'failed', 'standardError', 'standardOut'
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nIntegration Test result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacoco {
    toolVersion = jacocoToolVersion
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'
            excludes = ['*It.class', '*It.java']
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = jacocoInstructionCoverageMinimum
            }
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = jacocoLineCoverageMinimum
            }
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = jacocoMethodCoverageMinimum
            }
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = jacocoClassCoverageMinimum
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = jacocoBranchCoverageMinimum
            }
            limit {
                counter = 'COMPLEXITY'
                value = 'COVEREDRATIO'
                minimum = jacocoComplexityCoverageMinimum
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.enabled = true
    }
}

sonarqube {

}